;+
;*****************************************************************************************
;
;  FUNCTION :   read_digamma_ascii_file.pro
;  PURPOSE  :   This routine reads in the ASCII file of digamma numerical estimates
;                 given on a scale of 0.01 for the argument of the digamma function.
;
;  CALLED BY:   
;               lbw_digamma.pro
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               NA
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;               2)  ASCII files named
;                     digamma_numerical_estimates_0.01-41.0_0.01-steps.txt
;                     digamma_numerical_estimates_0.0001-0.1_0.0001-steps.txt
;                     digamma_numerical_estimates_0.05-0.15_0.0001-steps.txt
;                     digamma_numerical_estimates_0.09-10.10_0.01-steps.txt
;                     digamma_numerical_estimates_10.00-20.10_0.01-steps.txt
;                     digamma_numerical_estimates_20.00-30.10_0.01-steps.txt
;                     digamma_numerical_estimates_30.00-40.10_0.01-steps.txt
;                     digamma_numerical_estimates_40.0-101.0_0.05-steps.txt
;                   in the following directory
;                     ~/wind_3dp_pros/LYNN_PRO/general_math/
;
;  INPUT:
;               Z  :  Scalar [numeric] defining the argument of the digamma function
;
;  EXAMPLES:    
;               [calling sequence]
;               digam = read_digamma_ascii_file(z)
;
;  KEYWORDS:    
;               NA
;
;   CHANGED:  1)  Added more fine-grained ASCII files of shorter length to improve
;                   accuracy and speed up computation and now requires argument
;                   input to determine the range associated with a specific ASCII file
;                                                                   [04/12/2018   v1.1.0]
;             2)  Added error handling in case the input is somehow not within allowed
;                   bounds
;                                                                   [04/23/2018   v1.1.1]
;             8)  Added another ASCII file to extend argument range to 101.0
;                                                                   [04/23/2018   v1.1.2]
;
;   NOTES:      
;               1)  The Euler-Mascheroni constant to 50 decimal places is:
;                     0.57721566490153286060651209008240243104215933593992
;               2)  The ASCII file was generated by Mathematica to 20 decimal places
;               3)  User should not call this routine directly
;
;  REFERENCES:  
;               Equation (15) at:
;                 http://mathworld.wolfram.com/DigammaFunction.html
;
;   CREATED:  04/06/2018
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  04/23/2018   v1.1.2
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION read_digamma_ascii_file,z

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
zran           = [0.0003e0,100.09e0]
;;  Define save directory
slash          = get_os_slash()           ;;  '/' for Unix, '\' for Windows
vers           = !VERSION.OS_FAMILY       ;;  e.g., 'unix'
vern           = !VERSION.RELEASE         ;;  e.g., '7.1.1'
;;  Define the current working directory character
;;    e.g., './' [for unix and linux] otherwise guess './' or '.\'
test           = (vern[0] GE '6.0')
IF (test[0]) THEN cwd_char = FILE_DIRNAME('',/MARK_DIRECTORY) ELSE cwd_char = '.'+slash[0]
;;  Define current working directory
cur_wdir       = add_os_slash(FILE_EXPAND_PATH(cwd_char[0]))
;;  Define location of UMN Modified Wind/3DP IDL Libraries
tdir           = add_os_slash(FILE_EXPAND_PATH(cur_wdir[0]+'wind_3dp_pros'+slash[0]))
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
IF ((N_PARAMS() NE 1)) THEN RETURN,0b
;;  Define argument
zz             = 1d0*z[0]
CASE 1 OF
  (zz[0] GT 0.000 AND zz[0] LT 0.055) : BEGIN
    file_name      = 'digamma_numerical_estimates_0.0001-0.1_0.0001-steps.txt'
    mform          = '(f7.5,f30.20)'
  END
  (zz[0] GE 0.055 AND zz[0] LT 0.140) : BEGIN
    file_name      = 'digamma_numerical_estimates_0.05-0.15_0.0001-steps.txt'
    mform          = '(f7.5,f30.20)'
  END
  (zz[0] GE 0.140 AND zz[0] LT 10.05) : BEGIN
    file_name      = 'digamma_numerical_estimates_0.09-10.10_0.01-steps.txt'
    mform          = '(f5.2,f30.20)'
  END
  (zz[0] GE 10.05 AND zz[0] LT 20.00) : BEGIN
    file_name      = 'digamma_numerical_estimates_10.00-20.10_0.01-steps.txt'
    mform          = '(f5.2,f30.20)'
  END
  (zz[0] GE 20.05 AND zz[0] LT 30.00) : BEGIN
    file_name      = 'digamma_numerical_estimates_20.00-30.10_0.01-steps.txt'
    mform          = '(f5.2,f30.20)'
  END
  (zz[0] GE 30.05 AND zz[0] LT 40.09) : BEGIN
    file_name      = 'digamma_numerical_estimates_30.00-40.10_0.01-steps.txt'
    mform          = '(f5.2,f30.20)'
  END
  (zz[0] GE 40.09 AND zz[0] LT 100.09) : BEGIN
    file_name      = 'digamma_numerical_estimates_40.0-101.0_0.05-steps.txt'
    mform          = '(f6.2,f30.20)'
  END
  ELSE                                : BEGIN
    ;;  Not currently allowed --> return to user
    zzstr          = STRTRIM(STRING(zz[0],FORMAT='(g30.8)'),2L)
    MESSAGE,'Argument [zz = '+zzstr[0]+'] is currently too large...',/INFORMATIONAL,/CONTINUE
    RETURN,0b
  END
ENDCASE
;;----------------------------------------------------------------------------------------
;;  Define file location and name
;;----------------------------------------------------------------------------------------
file_dir       = tdir[0]+'LYNN_PRO'+slash[0]+'general_math'+slash[0]
;file_name      = 'digamma_numerical_estimates_0.01-41.0_0.01-steps.txt'
gfile          = FILE_SEARCH(file_dir[0],file_name[0])
good           = WHERE(gfile NE '',gd)
IF (gd[0] GT 0) THEN BEGIN
  mfile           = gfile[good[0]]
ENDIF ELSE BEGIN
  MESSAGE,'No file was found...',/INFORMATIONAL,/CONTINUE
  RETURN,0b
ENDELSE
;;----------------------------------------------------------------------------------------
;;  Open file and read in data
;;----------------------------------------------------------------------------------------
;mform          = '(f5.2,f30.20)'
nl             = FILE_LINES(mfile[0])               ;;  # of lines in the ASCII file that is data
argz           = REPLICATE(d,nl[0])                 ;;  argument of digamma function
psiz           = REPLICATE(d,nl[0])                 ;;  result of digamma function
aa             = REPLICATE(d,2)

OPENR,gunit,mfile[0],ERROR=err,/GET_LUN
IF (err[0] NE 0) THEN PRINT, -2, !ERROR_STATE.MSG   ;;  Prints an error message
FOR j=0L, nl[0] - 1L DO BEGIN
  READF,gunit[0],FORMAT=mform,aa
  argz[j]        = aa[0]
  psiz[j]        = aa[1]
ENDFOR
;;----------------------------------------------------------------------------------------
;;  Close file
;;----------------------------------------------------------------------------------------
FREE_LUN,gunit

;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN,[[argz],[psiz]]
END
