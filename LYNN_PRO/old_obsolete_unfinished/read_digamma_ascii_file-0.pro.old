;+
;*****************************************************************************************
;
;  FUNCTION :   read_digamma_ascii_file.pro
;  PURPOSE  :   This routine reads in the ASCII file of digamma numerical estimates
;                 given on a scale of 0.01 for the argument of the digamma function.
;
;  CALLED BY:   
;               NA
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               NA
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;               2)  ASCII file named
;                     digamma_numerical_estimates_0.01-41.0_0.01-steps.txt
;                   in the following directory
;                     ~/wind_3dp_pros/LYNN_PRO/general_math/
;
;  INPUT:
;               NA
;
;  EXAMPLES:    
;               [calling sequence]
;               digam = read_digamma_ascii_file()
;
;  KEYWORDS:    
;               NA
;
;   CHANGED:  1)  NA
;                                                                   [MM/DD/YYYY   v1.0.0]
;
;   NOTES:      
;               1)  The Euler-Mascheroni constant to 50 decimal places is:
;                     0.57721566490153286060651209008240243104215933593992
;               2)  The ASCII file was generated by Mathematica to 20 decimal places
;
;  REFERENCES:  
;               Equation (15) at:
;                 http://mathworld.wolfram.com/DigammaFunction.html
;
;   CREATED:  04/06/2018
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  04/06/2018   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION read_digamma_ascii_file

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
;;  Define save directory
slash          = get_os_slash()           ;;  '/' for Unix, '\' for Windows
vers           = !VERSION.OS_FAMILY       ;;  e.g., 'unix'
vern           = !VERSION.RELEASE         ;;  e.g., '7.1.1'
;;  Define the current working directory character
;;    e.g., './' [for unix and linux] otherwise guess './' or '.\'
test           = (vern[0] GE '6.0')
IF (test[0]) THEN cwd_char = FILE_DIRNAME('',/MARK_DIRECTORY) ELSE cwd_char = '.'+slash[0]
;;  Define current working directory
cur_wdir       = add_os_slash(FILE_EXPAND_PATH(cwd_char[0]))
;;  Define location of UMN Modified Wind/3DP IDL Libraries
tdir           = add_os_slash(FILE_EXPAND_PATH(cur_wdir[0]+'wind_3dp_pros'+slash[0]))
;;----------------------------------------------------------------------------------------
;;  Define file location and name
;;----------------------------------------------------------------------------------------
file_dir       = tdir[0]+'LYNN_PRO'+slash[0]+'general_math'+slash[0]
file_name      = 'digamma_numerical_estimates_0.01-41.0_0.01-steps.txt'
gfile          = FILE_SEARCH(file_dir[0],file_name[0])
good           = WHERE(gfile NE '',gd)
IF (gd[0] GT 0) THEN BEGIN
  mfile           = gfile[good[0]]
ENDIF ELSE BEGIN
  MESSAGE,'No file was found...',/INFORMATIONAL,/CONTINUE
  RETURN,0b
ENDELSE
;;----------------------------------------------------------------------------------------
;;  Open file and read in data
;;----------------------------------------------------------------------------------------
mform          = '(f5.2,f30.20)'
nl             = FILE_LINES(mfile[0])               ;;  # of lines in the ASCII file that is data
argz           = REPLICATE(d,nl[0])                 ;;  argument of digamma function
psiz           = REPLICATE(d,nl[0])                 ;;  result of digamma function
aa             = REPLICATE(d,2)

OPENR,gunit,mfile[0],ERROR=err,/GET_LUN
IF (err[0] NE 0) THEN PRINT, -2, !ERROR_STATE.MSG   ;;  Prints an error message
FOR j=0L, nl[0] - 1L DO BEGIN
  READF,gunit[0],FORMAT=mform,aa
  argz[j]        = aa[0]
  psiz[j]        = aa[1]
ENDFOR
;;----------------------------------------------------------------------------------------
;;  Close file
;;----------------------------------------------------------------------------------------
FREE_LUN,gunit

;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN,[[argz],[psiz]]
END
