;+
;*****************************************************************************************
;
;  FUNCTION :   lbw_el_response.pro
;  PURPOSE  :   This routine computes the response of the EESA Low electrostatic
;                 analyzer.
;
;  CALLED BY:   
;               
;
;  INCLUDES:
;               NA
;
;  COMMON BLOCKS:
;               LBW_EL_RESPONSE_COM
;
;  CALLS:
;               dgen.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               VOLTS      :  [A,E]-Element [numeric] array of voltages [V] at which
;                               to numerically calculate the electron instrument response
;                                 A  :  # of anodes
;                                 E  :  # of energy bins
;               ENERGY     :  [E,S]-Element [numeric] array of energies [eV] at which
;                               to numerically calculate the electron instrument response
;                                 E  :  # of energy bins
;                                 S  :  # of solid angles
;               DENERGY    :  [E,S]-Element [numeric] array of energy bin widths [eV]
;                               at which to numerically calculate the electron
;                               instrument response
;                                 E  :  # of energy bins
;                                 S  :  # of solid angles
;
;  EXAMPLES:    
;               [calling sequence]
;               resp = lbw_el_response(volts, energy, denergy [,NSTEPS=nsteps])
;
;  KEYWORDS:    
;               NSTEPS     :  Scalar [numeric] value defining the number of energy
;                               bins
;
;   CHANGED:  1)  Routine last modified by ??
;                                                                   [??/??/????   v1.0.0]
;             2)  Routine updated and renamed from el_response.pro to lbw_el_response.pro
;                   and now does all computations in double-precision
;                                                                   [02/28/2019   v1.0.1]
;             3)  Continued to update routine and cleaned up a bit
;                                                                   [02/28/2019   v1.0.2]
;
;   NOTES:      
;               1)  MCP Design:
;                     A MCP consists of a series of small (5 μm to 0.25 mm diameter) holes
;                     (or channels) in a thin plate (typically 0.4-3.0 mm thick) made of a
;                     conducting material specially fabricated to produce signals similar
;                     to a secondary electron analyzer.  MCPs are often used in pairs
;                     where a cross-sectional cut through two connecting channels creates
;                     a v-shaped tube, called a chevron pair. This prevents incident
;                     particles from directly impacting the detector behind the plates.
;                     When a particle impacts the channel wall, if it has enough energy,
;                     it will produce a shower of electrons. The number of electrons per
;                     incident particle impact is referred to as the gain of the detector.
;                     --  E = q (V_2 - V_1)/[2 ln|R_2/R_1|]
;                           E    :  energy of incident particle allowed by analyzer
;                           q    :  charge of incident particle
;                           V_j  :  voltage on jth electrode (2 = outer)
;                           R_j  :  radius of jth hemisphere (2 = outer)
;                     --  k = <R>/∆R or K = E/∆V
;                           k    :  analyzer constant
;                           K    :  analyzer constant
;                           <R>  :  mean radius = (R_1 + R_2)/2
;                           ∆R   :  gap distance = (R_2 - R_1)
;                           ∆V   :  gap distance = (V_2 - V_1)
;               2)  MCP Notes:
;                     --  "MCPs are electron multipliers produced by a voltage bias
;                          across a resistive glass tube that generates an electron
;                          cascade through secondary electron production..."
;                          [Wüest, Evans, and von Steiger, 2007, pgs 18-19, Ch 2]
;                     --  "MCPs consist of an array of microscopic glass tubes (typically
;                          12-25 µm spacing), hexagonally packed...and sliced as thin
;                          wafers (0.5 or 1.0 mm thick) with typical microchannel length
;                          to diameter (L:D) ratios between 40:1 and 80:1..."
;                          [Wüest, Evans, and von Steiger, 2007, pg 19, Ch 2]
;                     --  "MCP wafers (typically 0.5 or 1.0 mm thick) are sliced at a
;                          small bias angle (typically 8-12 deg) relative to the
;                          microchannel axis.  They are stacked in pairs (Chevron
;                          configuration) or in triplets (Z-stack), with adjacent wafers
;                          having opposite bias angles...to prevent ion feedback..."
;                          [Wüest, Evans, and von Steiger, 2007, pg 19, Ch 2]
;                     --  "The bias voltage is generally chosen so the secondary electron
;                          production at the back of the MCP stack is near the
;                          microchannel saturation, resulting in a roughly fixed charge
;                          per microchannel firing..."
;                          [Wüest, Evans, and von Steiger, 2007, pg 19, Ch 2]
;                     --  "Pulse height distributions (PHDs) with a roughly Gaussian
;                          shape and a FWHM equivalent to ∼50–100% of the peak height
;                          are typical, with the FWHM depend- ing upon the MCP gain..."
;                          [Wüest, Evans, and von Steiger, 2007, pg 19, Ch 2]
;                     --  "For high count rates, discrete anodes are preferred since
;                          individual preamps are readily available that count at
;                          ~10^(7) counts per second allowing total instrument rates of
;                          ~10^(8) counts per second."
;                          [Wüest, Evans, and von Steiger, 2007, pgs 21-22, Ch 2]
;                     --  "A general rule of thumb is to try to keep the average charge
;                          pulse current at the highest counting rates (using nominal
;                          gain) to less than 20% of the MCP current."
;                          [Wüest, Evans, and von Steiger, 2007, pg 23, Ch 2]
;                     --  Corning 8161 Glass [aka Potash Rubium Lead]
;                           rho     :  Density ~ 3.98 ± 0.01 g cm^(-3)
;                           ∑       :  Dielectric Constant ~ 8.3
;                                      UCLA Tech. Rep.:
;                                           ∑    ~ 8.35--8.59
;                                           LF   ~ 0.50--0.76%
;                                           dmax ~ 2.9--3.5
;                           LF x C  :  loss factor (LF) times capacitance ~ 4-5
;                                        [ C ~ ∑ * 0.225/wall thickness ]
;                           Composition (company's results):
;                           PbO     :  51.40%    [lead monoxide]
;                           SiO2    :  38.70%    [silicon dioxide or silica]
;                           K2O     :   6.60%    [potassium oxide or Kalium oxide]
;                           BaO     :   2.00%    [barium oxide]
;                           Sb2O3   :   0.38%    [antimonous oxide]
;                           CaO     :   0.30%    [calcium oxide]
;                           Al2O3   :   0.20%    [aluminum oxide or alumina]
;                           Na2O    :   0.20%    [sodium oxide]
;                           MgO     :   0.04%    [magnesium oxide or magnesia]
;                           As2O3   :   0.04%    [arsenic trioxide]
;                     --  T_c = R_c C_c ~ K d [Ladislas Wiza, 1979]
;                           T_c  :  channel recovery time ~ 10-20 ms per channel
;                           R_c  :  effective single channel resistance ~ 2.75 x 10^(14) Ω
;                           C_c  :  effective single channel capacitance ~ 7.27 x 10^(-17) F
;                           K    :  proportionality constant ~ 4 x 10^(-13)
;                           d    :  channel diameter ~ 10--100 µm
;                           
;                           
;               3)  Poisson Statistics
;                       P_n    :  the probability to emit n electrons
;                       G_e    :  mean number of emitted electrons = secondary electron yield
;                       n      :  # of emitted electrons
;                       P_n = (G_e^n)/n! e^(-G_e)
;                     Barat et al. [2000] found that G_e depends upon the mass, Mi, and
;                     velocity, Vi, of the incident particle finding:
;                       G_e = Mi^(ß) [Vi - Vo]
;                     where Vo = Vo(Mi) is a constant and they find ß ~ 0.9 for speeds
;                     between ~50-150 km/s for Na and K, where Vo ≤ 40 km/s and
;                     M is some goofy mass unit, e.g., C-60 has M ~ 720u.
;
;  REFERENCES:  
;               1)  Carlson et al., "An instrument for rapidly measuring plasma
;                      distribution functions with high resolution,"
;                      Adv. Space Res. Vol. 2, pp. 67-70, 1983.
;               2)  Curtis et al., "On-board data analysis techniques for space plasma
;                      particle instruments," Rev. Sci. Inst. Vol. 60,
;                      pp. 372, 1989.
;               3)  Lin et al., "A Three-Dimensional Plasma and Energetic particle
;                      investigation for the Wind spacecraft," Space Sci. Rev.
;                      Vol. 71, pp. 125, 1995.
;               4)  Paschmann, G. and P.W. Daly "Analysis Methods for Multi-Spacecraft
;                      Data," ISSI Scientific Report, Noordwijk, 
;                      The Netherlands., Int. Space Sci. Inst., 1998.
;               5)  R.R. Goruganthu and W.G. Wilson "Relative electron detection
;                      efficiency of microchannel plates from 0-3 keV,"
;                      Rev. Sci. Instrum. Vol. 55(12), pp. 2030--2033,
;                      doi:10.1063/1.1137709, 1984.
;               6)  F. Bordoni "Channel electron multiplier efficiency for 10-1000 eV
;                      electrons," Nucl. Inst. & Meth. Vol. 97, pp. 405--408,
;                      doi:10.1016/0029-554X(71)90300-4, 1971.
;               7)  M. Wüest, D.S. Evans, and R. von Steiger "Calibration of Particle
;                      Instruments in Space Physics," ESA Publications Division,
;                      Keplerlaan 1, 2200 AG Noordwijk, The Netherlands, 2007.
;               8)  M. Wüest, et al., "Review of Instruments," ISSI Sci. Rep. Ser.
;                      Vol. 7, pp. 11--116, 2007.
;               9)  J. Ladislas Wiza "Microchannel plate detectors," Nucl. Inst. & Meth.
;                      Vol. 162(1), pp. 587--601, doi:10.1016/0029-554X(79)90734-1, 1979.
;              10)  J.D. Mackenzie "MCP Glass Analysis Studies" Technical Report,
;                      Contract No. DAAG 53-75-C-0222, prepared for Night Vision Lab.,
;                      prepared by J.D. Mackenzie at UCLA.
;              11)  R.S. Gao, et al., "Absolute and angular efficiencies of a
;                      microchannel-plate position-sensitive detector," Rev. Sci. Inst.
;                      Vol. 55(11), pp. 1756--1759, doi:10.1063/1.1137671, 1984.
;              11)  M. Barat, J.C. Brenot, J A. Fayeton, and Y.J. Picard "Absolute
;                      detection efficiency of a microchannel plate detector for
;                      neutral atoms," Rev. Sci. Inst. Vol. 71(5), pp. 2050--2052,
;                      doi:10.1063/1.1150615, 2000.
;              12)  A. Brunelle, et al., "Secondary Electron Emission Yields from a CsI
;                      Surface Under Impacts of Large Molecules at Low Velocities
;                      (5–70 km/s)", Rapid Commun. Mass Spectrom. Vol. 11(4),
;                      pp. 353--362, 1997.
;
;   ADAPTED FROM: el_response.pro    BY: Davin Larson
;   CREATED:  02/28/2019
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  02/28/2019   v1.0.2
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION lbw_el_response,volts,energy,denergy,NSTEPS=nsteps

;;----------------------------------------------------------------------------------------
;;  Constants
;;----------------------------------------------------------------------------------------
;;  Instrumental
k_an           = 6.42d0        ;;  ESA instrument analyzer constant (energy [eV] per volt)
fwhm           = 0.22d0        ;;  Full Width at Half Maximum (correction?) [FWHM]
;;----------------------------------------------------------------------------------------
;;  Define common block
;;----------------------------------------------------------------------------------------
COMMON lbw_el_response_com,resp0,volts0,energy0,denergy0,nsteps0
;;----------------------------------------------------------------------------------------
;;  Check keywords
;;----------------------------------------------------------------------------------------
;;  Check NSTEPS0
IF KEYWORD_SET(nsteps0) THEN BEGIN
  IF (nsteps EQ nsteps0 AND TOTAL(volts ne volts0) EQ 0) THEN BEGIN
    ;;  Data was already computed for this set of voltages
    ;;    --> Redefine inputs and return
    energy  = energy0
    denergy = denergy0
    RETURN,resp0
  ENDIF
ENDIF
MESSAGE,/INFORMATIONAL,'Computing instrument response.'
;;  Check NSTEPS
IF KEYWORD_SET(nsteps) THEN BEGIN
  ;;  Define energies and energy bin widths
  v_ran   = [MIN(volts,/NAN),MAX(volts,/NAN)]
  erange  = k_an[0]*v_ran*([1d0 - fwhm[0],1d0 + fwhm[0]])^2d0
  energy  = dgen(nsteps,/LOG,RANGE=erange)
  i       = LINDGEN(nsteps)
  denergy = ABS(energy[i + 1L] - energy[i - 1L])/2d0
ENDIF
nn             = N_ELEMENTS(energy)       ;;  # of energy bins
nv             = N_ELEMENTS(volts)        ;;  # of voltages
dim            = SIZE(volts,/DIMENSIONS)
;;----------------------------------------------------------------------------------------
;;  Correct for analyzer constant
;;----------------------------------------------------------------------------------------
;;  Correct voltages by instrument constant
es             = REPLICATE(1.,nv[0]) # energy
kvs            = REFORM(k_an[0]*volts,nv[0]) # REPLICATE(1.,nn[0])
;;  Calculate variance of Gaussian
;;    FWHM = 2 (ln|2|)^(1/2) V_th
sig            = fwhm[0]/(2d0*SQRT(2d0*ALOG(2d0))) * kvs
;;----------------------------------------------------------------------------------------
;;  Compute instrument response
;;----------------------------------------------------------------------------------------
;;  Compute initial response
resp           = EXP(-((es - kvs)/sig)^2d0/2d0) / SQRT(2d0*!DPI)/sig
;;  Finalize response
resp          *= (REPLICATE(1.,nv[0]) # denergy)
;;  Check if response needs to be resized and summed
IF (N_ELEMENTS(dim) EQ 2) THEN BEGIN
  ;;  Resize array
  resp        = REFORM(resp,dim[0],dim[1],nn[0])
  ;;  Sum over 1st dimension (should be the anodes)
  resp        = TOTAL(resp,1,/NAN)/dim[0]
ENDIF
;;----------------------------------------------------------------------------------------
;;  Redefine common block variables
;;----------------------------------------------------------------------------------------
volts0         = volts
resp0          = resp
energy0        = energy
denergy0       = denergy
nsteps0        = nsteps
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN,resp
END



;plot,energy,resp(14,*),/xl
;for i=0,dimen1(resp)-1 do oplot,energy,resp(i,*)

